import org.gradle.api.internal.project.ProjectInternal

/*
 * Copyright 2019 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        gradlePluginPortal()
    }
    
    dependencies {
        classpath 'com.netflix.nebula:gradle-netflixoss-project-plugin:11.5.0'
        classpath 'com.bmuschko:gradle-docker-plugin:6.7.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.1.0'
    }
}

apply plugin: 'nebula.netflixoss'
apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'pmd'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'
apply plugin: 'com.palantir.git-version'

ext {
    archaiusVersion = '2.3.+'
    assetjCoreVersion = '3.11.1'
    guavaVersion = '19.+'
    gsonVersion = '2.8.+'
    guiceVersion = '4.1.0'
    jettyVersion = '9.4.12.v20180830'
    jsonVersion = '20160810'
    junitVersion = '4.10'
    lombokVersion = '1.18.10'
    mantisVersion = '2.0.98'
    mockitoVersion = '3.+'
    s3Version = '1.11.566'
    servletApiVersion = '3.1.0'
    spectatorVersion = '0.92.+'
    vavrVersion = '0.10.2'
    zuulVersion = '2.3.0'
}

group = 'io.mantisrx'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        url "https://netflixoss.jfrog.io/artifactory/maven-oss-candidates"
    }
    maven {
        url "https://artifacts-oss.netflix.net/artifactory/maven-oss-snapshots"
    }
}

tasks.withType(Javadoc).all {
    enabled = false
}


dependencies {
    api("com.netflix.zuul:zuul-core:${zuulVersion}") {
        attributes { attribute(ProjectInternal.STATUS_ATTRIBUTE, 'release') }
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.inject', module: 'guice'
    }
    api("com.netflix.zuul:zuul-guice:${zuulVersion}") {
        attributes { attribute(ProjectInternal.STATUS_ATTRIBUTE, 'release') }
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.inject', module: 'guice'
    }
    implementation("com.netflix.zuul:zuul-groovy:${zuulVersion}") {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.inject', module: 'guice'
    }
    implementation 'com.netflix.blitz4j:blitz4j:1.37.2'
    implementation ("com.netflix.governator:governator:1.+") {
        attributes { attribute(ProjectInternal.STATUS_ATTRIBUTE, 'release') }
    }
    implementation "org.apache.commons:commons-lang3:3.+"
    implementation "commons-configuration:commons-configuration:1.8"
    compileOnly "com.netflix.ribbon:ribbon-loadbalancer:2.4.4"
    compileOnly "com.netflix.ribbon:ribbon-eureka:2.4.4"
    api "io.mantisrx:mantis-discovery-proto:$mantisVersion"
    api "io.mantisrx:mantis-client:$mantisVersion"
    api "io.mantisrx:mantis-runtime:$mantisVersion"
    api "io.mantisrx:mantis-control-plane-client:$mantisVersion"
    api "io.mantisrx:mantis-server-worker-client:$mantisVersion"
    api "io.mantisrx:mql-jvm:3.+"
    api "io.vavr:vavr:$vavrVersion"
    api "com.google.inject:guice:$guiceVersion"
    api "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation"org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
}


configurations.all {
    resolutionStrategy {
        force "com.google.guava:guava:$guavaVersion"
        force "com.google.inject:guice:$guiceVersion"
        force "com.google.inject.extensions:guice-multibindings:$guiceVersion"
        force "com.google.inject.extensions:guice-grapher:$guiceVersion"
        force "com.google.inject.extensions:guice-assistedinject:$guiceVersion"
        force "javax.servlet:javax.servlet-api:$servletApiVersion"
    }
}

/*
 * Run regular:   ./gradlew run
 * Run benchmark: ./gradlew run -Pbench
 */
run {
    mainClassName = "io.mantisrx.api.Bootstrap"

    applicationDefaultJvmArgs = ["-DTZ=GMT",
                                 "-Darchaius.deployment.environment=test",
                                 "-Dcom.sun.management.jmxremote",
                                 "-Dcom.sun.management.jmxremote.local.only=false",
                                 "-Deureka.validateInstanceId=false",
                                 "-Deureka.mt.num_retries=1"]

    if (project.hasProperty('bench')) {
        println 'Running benchmark configuration...'
        jvmArgs "-Darchaius.deployment.environment=benchmark"
    }
}

def installDir = file("${buildDir}/install")
def resourcesDir = file("${buildDir}/resources")
def ci = System.getenv('GITHUB_ACTIONS')
def imageRepository = ci ? 'netflixoss' : 'localhost:5001/netflixoss'
def dockerVersionTag = gitVersion()

docker {
    dockerSyncBuildContext {
        from installDir
        from resourcesDir
    }

    dockerCreateDockerfile {
        // root directory to store all the files
        instruction 'WORKDIR /apps/mantis/mantis-api'
        // copy the files from the build context to the image
        instruction 'COPY mantis-api/bin/* /apps/mantis/mantis-api/bin/'
        instruction 'COPY mantis-api/lib/* /apps/mantis/mantis-api/lib/'
        instruction 'COPY resources/* conf/'
        entryPoint 'bin/mantis-api', '-p', 'api-docker.properties'
    }

    javaApplication {
        baseImage = 'azul/zulu-openjdk:8-latest'
        maintainer = 'Mantis Developers "mantis-oss-dev@netflix.com"'
        mainClassName = 'io.mantisrx.api.Bootstrap'
        images = ["$imageRepository/mantisapi:latest", "$imageRepository/mantisapi:$dockerVersionTag"]
        ports = [5050]
    }
}

dockerSyncBuildContext.dependsOn(installDist)

pmd {
    ignoreFailures = true
}
